buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.1.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'crashlytics'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "me.vickychijwani.spectre"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0-beta"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE"))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // reactive extensions (rx)
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.trello:rxlifecycle:0.1.0'
    compile 'com.trello:rxlifecycle-components:0.1.0'

    // networking / communication / data storage
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.realm:realm-android:0.80.1'
    compile 'com.squareup:otto:1.3.6'

    // UI
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'
    compile 'com.splitwise:tokenautocomplete:1.3.3@aar'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.0.Final'
    compile 'com.tsengvn:Typekit:1.0.0'

    // misc
    compile 'com.jakewharton:butterknife:7.0.0'
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    compile 'com.github.slugify:slugify:2.1.3'

    // debug
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.1.1'
    debugCompile 'com.uphyca:stetho_realm:0.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    debugCompile 'io.palaima.debugdrawer:debugdrawer:0.3.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-okhttp:0.3.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-picasso:0.3.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-scalpel:0.3.0'
}
